int_to_str_list : Template {
	value :
		If digits < 0
			Then
				Error "The number of digits must be non-negative."
			Else
				(For arg : args, arg_name : Name
				Select arg_name : XInvoke flabbergast.Stringish fromInt (arg To Int, hex, digits))
}
float_to_str_list : Template {
	value :
		If digits < 0
			Then
				Error "The number of digits must be non-negative."
			Else
				(For arg : args, arg_name : Name
				Select arg_name : XInvoke flabbergast.Stringish fromDouble (arg To Float, exponential, digits))
}

str_categories_list : Template {
	value :
		For arg : args, arg_name : Name
		Select arg_name : XFuture flabbergast.CharacterCategory
}
str_codepoints_list : Template {
	value :
		For arg : args, arg_name : Name
		Select arg_name : XFuture flabbergast.StringToCodepoints
}

str_escape_list : Template {
	value : XFuture flabbergast.Escape
}
str_transform : {
	char_tmpl : Template {
		type : 0
	}
	range_tmpl : Template {
		type : 1
		mode_hex_upper : { four : "08X"  two : "04X"  one : "02X" }
		mode_hex_lower : { four : "08x"  two : "04x"  one : "02x" }
		mode_decimal : { four : "d"  two : "d"  one : "d" }

		utf32 : { value : "%1$\(mode.four)" }
		utf16_0 : { value : "%2$\(mode.two)" }
		utf16_1 : { value : "%3$\(mode.two)" }
		utf8_0 : { value : "%4$\(mode.one)" }
		utf8_1 : { value : "%5$\(mode.one)" }
		utf8_2 : { value : "%6$\(mode.one)" }
		utf8_3 : { value : "%7$\(mode.one)" }

		format_str :
			For part : replacement
				Reduce
					acc &
					(If part Is Frame
							Then part.value Enforce Str
							Else XInvoke java.lang.String replaceAll (part To Str, "%", "%%"))
				With acc : ""
	}
}
str_find_list : Template {
	value :
		If Length str == 0
			Then Error "“str” must not be empty."
			Else
				(For arg : str_args, arg_name : Name
				Select arg_name :
					If arg.enabled
					Then
						(Let pos :
							If backward
								Then XInvoke java.lang.String lastIndexOf (arg.str, str To Str, XInvoke java.lang.String offsetByCodePoints (arg.str, 0, arg.real_start))
								Else XInvoke java.lang.String indexOf (arg.str, str To Str, XInvoke java.lang.String offsetByCodePoints (arg.str, 0, arg.real_start))
						In If pos == -1 Then Null Else pos)
					Else Null)
}

str_replace_list : Template {
	value :
		If Length str == 0
			Then Error "“str” must not be empty."
			Else
				(Let
					java_pattern : XInvoke java.util.regex.Pattern quote (str To Str),
					java_replacement : XInvoke java.util.regex.Matcher quoteReplacement (with To Str)
				In
					For arg : args, arg_name : Name
					Select arg_name : XInvoke java.lang.String replaceAll (arg To Str, java_pattern, java_replacement))
}
str_slice_list : Template {
	value :
		For arg : str_args, arg_name : Name
		Select arg_name :
			If arg.enabled
				Then XInvoke java.lang.String substring (arg.str,  XInvoke java.lang.String offsetByCodePoints (arg.str, 0, arg.real_start), XInvoke java.lang.String offsetByCodePoints (arg.str, 0, arg.real_start + arg.real_length))
				Else empty
}

bin_to_hex_str_list : Template {
	value :
		For arg : args, arg_name : Name
		Select arg_name : XInvoke flabbergast.BinaryFunctions bytesToHex (arg, delimiter, uppercase)
}


str_punycode_list : Template {
	value :
		For arg : args, arg_name : Name
		Select arg_name : XFuture flabbergast.Punycode
}
